/*
 * Copyright (c) 2017-2025 The Forge Interactive Inc.
 * 
 * This file is part of The-Forge
 * (see https://github.com/ConfettiFX/The-Forge).
 * 
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 * 
 *   http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
*/

// USERMACRO: SAMPLE_COUNT [1,2,4]
// Uncomment this definition to use ray differentials method for calculating
// gradients instead of screen-space projected triangles method.
// #define USE_RAY_DIFFERENTIALS

#include "Shading.h.fsl"
#include "../../../../../Common_3/Renderer/VisibilityBuffer/Shaders/FSL/VisibilityBufferShadingUtilities.h.fsl"

#include "VisibilityBufferResources.h.fsl"
#include "GLobal.srt.h"

// This shader loads draw / triangle Id per pixel and reconstruct interpolated vertex data.

STRUCT(VSOutput)
{
	DATA(float4, position, SV_Position);
	DATA(float2, screenPos, TEXCOORD0);
#if FT_MULTIVIEW
    DATA(FLAT(uint), ViewID, TEXCOORD3);
#endif
};

// Static descriptors

#if(SAMPLE_COUNT > 1)
EARLY_FRAGMENT_TESTS
#endif
ROOT_SIGNATURE(DefaultRootSignature)
float4 PS_MAIN( VSOutput In, SV_SampleIndex(uint) sampleIndex )
{
	INIT_MAIN;
	// Load Visibility Buffer raw packed float4 data from render target
#if(SAMPLE_COUNT > 1)
#if FT_MULTIVIEW
	float4 visRaw = LoadTex2DArrayMS(gVBTex, gDepthSampler, uint3(In.position.xy, In.ViewID), sampleIndex);
#else
	float4 visRaw = LoadTex2DMS(gVBTex, gDepthSampler, uint2(In.position.xy), sampleIndex);
#endif /*FT_MULTIVIEW*/
#else
#if FT_MULTIVIEW
	float4 visRaw = LoadTex3D(gVBTex, gDepthSampler, uint3(In.position.xy, In.ViewID), 0);
#else
	float4 visRaw = LoadTex2D(gVBTex, gDepthSampler, uint2(In.position.xy), 0);
#endif/*FT_MULTIVIEW*/
#endif/*SAMPLE_COUNT > 1*/

	// Unpack float4 render target data into uint to extract data
	uint geomSetPrimID = packUnorm4x8(visRaw);

	// Early exit if this pixel doesn't contain triangle data
	if (geomSetPrimID == ~0u)
	{
#if FT_MULTIVIEW
	float4x4 invProjViewOrigin = gUniformCameraSky.invProjViewOrigin.mat[In.ViewID];
#else
	float4x4 invProjViewOrigin = gUniformCameraSky.invProjViewOrigin.mat;
#endif /*FT_MULTIVIEW*/
		float3 skyTexCoord = mul(invProjViewOrigin, float4(In.screenPos.x, In.screenPos.y, 1, 1)).xyz;
		float3 FinalColor = SampleTexCube(gSkyboxTex, gSamplerBilinearWrap, skyTexCoord).xyz;
		RETURN(float4(FinalColor, 1.0));
	}

	// Extract packed data
	uint primitiveID = (geomSetPrimID >> PRIM_ID_LOW_BIT) & PRIM_ID_MASK;
	uint geomSet = (geomSetPrimID >> GEOM_LOW_BIT) & GEOM_MASK;

	uint triIdx0 = INDEXBUFFER_OFFSET(geomSet) + (primitiveID * 3 + 0);
	uint triIdx1 = INDEXBUFFER_OFFSET(geomSet) + (primitiveID * 3 + 1);
	uint triIdx2 = INDEXBUFFER_OFFSET(geomSet) + (primitiveID * 3 + 2);

	uint index0 = LoadByte(gFilteredIndexBuffer, triIdx0 << 2);
	uint index1 = LoadByte(gFilteredIndexBuffer, triIdx1 << 2);
	uint index2 = LoadByte(gFilteredIndexBuffer, triIdx2 << 2);

	// Load vertex data of the 3 vertices
	float3 v0pos = asfloat(LoadByte4(gVertexPositionBuffer, index0 * 12)).xyz;
	float3 v1pos = asfloat(LoadByte4(gVertexPositionBuffer, index1 * 12)).xyz;
	float3 v2pos = asfloat(LoadByte4(gVertexPositionBuffer, index2 * 12)).xyz;

#if FT_MULTIVIEW
	float4x4 projInv = InverseProjectionMatrixPerspectiveReverseZ(gPerFrameVBConstants.transform[VIEW_CAMERA].projection.mat[In.ViewID]);
	float4x4 invVP = gPerFrameVBConstants.transform[VIEW_CAMERA].invVP.mat[In.ViewID];
	float4x4 viewInv = mul(invVP, gPerFrameVBConstants.transform[VIEW_CAMERA].projection.mat[In.ViewID]);
	float4x4 mvp = gPerFrameVBConstants.transform[VIEW_CAMERA].mvp.mat[In.ViewID];
#else
	float4x4 projInv = InverseProjectionMatrixPerspectiveReverseZ(gPerFrameVBConstants.transform[VIEW_CAMERA].projection.mat);
	float4x4 invVP = gPerFrameVBConstants.transform[VIEW_CAMERA].invVP.mat;
	float4x4 viewInv = mul(invVP, gPerFrameVBConstants.transform[VIEW_CAMERA].projection.mat);
	float4x4 mvp = gPerFrameVBConstants.transform[VIEW_CAMERA].mvp.mat;
#endif /*FT_MULTIVIEW*/

	// Transform positions to clip space
#if FT_MULTIVIEW
	float4 pos0 = mul(mvp, float4(v0pos, 1.0f));
	float4 pos1 = mul(mvp, float4(v1pos, 1.0f));
	float4 pos2 = mul(mvp, float4(v2pos, 1.0f));

	float4 wPos0 = mul(invVP, pos0);
	float4 wPos1 = mul(invVP, pos1);
	float4 wPos2 = mul(invVP, pos2);
#else
	float4 pos0 = mul(mvp, float4(v0pos, 1.0f));
	float4 pos1 = mul(mvp, float4(v1pos, 1.0f));
	float4 pos2 = mul(mvp, float4(v2pos, 1.0f));

	float4 wPos0 = mul(invVP, pos0);
	float4 wPos1 = mul(invVP, pos1);
	float4 wPos2 = mul(invVP, pos2);
#endif

	float2 two_over_windowsize = gPerFrameConstants.twoOverRes;

	// Compute partial derivatives and baycentric coordinates.
	// This is necessary to interpolate triangle attributes per pixel.
#if defined(USE_RAY_DIFFERENTIALS)
	BarycentricDeriv derivativesOut = CalcRayBary(wPos0.xyz, wPos1.xyz, wPos2.xyz, In.screenPos, gPerFrameConstants.camPos.xyz, viewInv, projInv, two_over_windowsize);
#else
	BarycentricDeriv derivativesOut = CalcFullBary(pos0, pos1, pos2, In.screenPos, two_over_windowsize);

#endif /*USE_RAY_DIFFERENTIALS*/

	f3x2 texCoords = make_f3x2_cols(
			unpack2Floats(LoadByte(gVertexTexCoordBuffer, index0 << 2)) ,
			unpack2Floats(LoadByte(gVertexTexCoordBuffer, index1 << 2)) ,
			unpack2Floats(LoadByte(gVertexTexCoordBuffer, index2 << 2)) 
	);

	// Interpolated 1/w (one_over_w) for all three vertices of the triangle
	// using the barycentric coordinates and the delta vector
	float w = dot(float3(pos0.w, pos1.w, pos2.w),derivativesOut.m_lambda);

	// Reconstruct the Z value at this screen point performing only the necessary matrix * vector multiplication
	// operations that involve computing Z
#if FT_MULTIVIEW
	float z = w * getElem(gPerFrameVBConstants.transform[VIEW_CAMERA].projection.mat[In.ViewID], 2, 2) + getElem(gPerFrameVBConstants.transform[VIEW_CAMERA].projection.mat[In.ViewID], 3, 2);
#else
	float z = w * getElem(gPerFrameVBConstants.transform[VIEW_CAMERA].projection.mat, 2, 2) + getElem(gPerFrameVBConstants.transform[VIEW_CAMERA].projection.mat, 3, 2);
#endif

	// Calculate the world position coordinates:
	// First the projected coordinates at this point are calculated using In.screenPos and the computed Z value at this point.
	// Then, multiplying the perspective projected coordinates by the inverse view-projection matrix (invVP) produces world coordinates

#if FT_MULTIVIEW
	float3 position = mul(gPerFrameVBConstants.transform[VIEW_CAMERA].invVP.mat[In.ViewID], float4(In.screenPos * w, z, w)).xyz;
#else
	float3 position = mul(gPerFrameVBConstants.transform[VIEW_CAMERA].invVP.mat, float4(In.screenPos * w, z, w)).xyz;
#endif

	/////////////LOAD///////////////////////////////
	uint materialID = gIndirectDataBuffer[index0];

	// Interpolate texture coordinates and calculate the gradients for texture sampling with mipmapping support
	GradientInterpolationResults results = Interpolate2DWithDeriv(derivativesOut,texCoords);
	
	float2 texCoordDX = results.dx;
	float2 texCoordDY = results.dy;
	float2 texCoord = results.interp;

	// CALCULATE PIXEL COLOR USING INTERPOLATED ATTRIBUTES
	// Reconstruct normal map Z from X and Y
	// "NonUniformResourceIndex" is a "pseudo" function see
	// http://asawicki.info/news_1608_direct3d_12_-_watch_out_for_non-uniform_resource_index.html

	// Get textures from arrays.
	float4 normalMapData;
	float4 diffuseColor;
	float4 specularColor;
	BeginNonUniformResourceIndex(materialID, MAX_TEXTURE_UNITS);
		normalMapData = SampleGradTex2D(gNormalMaps[materialID],   gTextureSampler, texCoord, texCoordDX, texCoordDY);
		diffuseColor  = SampleGradTex2D(gDiffuseMaps[materialID],  gTextureSampler, texCoord, texCoordDX, texCoordDY);
		specularColor = SampleGradTex2D(gSpecularMaps[materialID], gTextureSampler, texCoord, texCoordDX, texCoordDY);
	EndNonUniformResourceIndex();

#if defined(ENABLED_GODRAY)
	// Sample Godray
	float3 godrayColor = SampleLvlTex2D(gGodRayTexture, gTextureSampler, In.screenPos.xy * float2(0.5, -0.5) + 0.5, 0).rgb;
	godrayColor *= gPerFrameConstants.lightColor.rgb * gPerFrameConstants.godrayAttenuation * (1.0 / GODRAY_STEPS);
#endif

	// reconstruct encoded normals...
	float3 reconstructedNormalMap;
	reconstructedNormalMap.xy = normalMapData.ga * 2.0f - 1.0f;
	reconstructedNormalMap.z = sqrt(saturate(1.0f - dot(reconstructedNormalMap.xy, reconstructedNormalMap.xy)));

	// NORMAL INTERPOLATION
	float3x3 normals = make_f3x3_rows(
		decodeDir(unpackUnorm2x16(LoadByte(gVertexNormalBuffer, index0 << 2))),
		decodeDir(unpackUnorm2x16(LoadByte(gVertexNormalBuffer, index1 << 2))),
		decodeDir(unpackUnorm2x16(LoadByte(gVertexNormalBuffer, index2 << 2)))
	);
	float3 normal = normalize(InterpolateWithDeriv_float3x3(derivativesOut, normals));
	
	// Calculate pixel normal and tangent vectors
	f3x3 wPositions = make_f3x3_cols(
			wPos0.xyz,
			wPos1.xyz,
			wPos2.xyz
	);

	DerivativesOutput wPosDer = Cal3DDeriv(derivativesOut, wPositions);
	DerivativesOutput uvDer = { float3(results.dx, 0.0),  float3(results.dy, 0.0) };
	normal = perturb_normal(reconstructedNormalMap, normal, wPosDer, uvDer);

	float Roughness = clamp(specularColor.a, 0.05f, 0.99f);
	float Metallic = specularColor.b;

#if USE_AMBIENT_OCCLUSION
	float ao = calculateAoContrib(
		gPerFrameConstants.depthTexSize,
		gPerFrameConstants.aoIntensity,
		gPerFrameConstants.aoQuality,
		gPerFrameConstants.CameraPlane.y,
		gPerFrameConstants.CameraPlane.x,
		gPerFrameConstants.frustumPlaneSizeNormalized,
		In.position.xy,
		gDepthTex,
#if FT_MULTIVIEW
		In.ViewID,
#endif
		gDepthSampler);
#else
	float ao = 1.0f;
#endif

	bool isTwoSided = (geomSet == GEOMSET_ALPHA_CUTOUT) && bool(gMeshConstantsBuffer[materialID].twoSided);
	float3 ViewVec = normalize(gPerFrameConstants.camPos.xyz - position);
	float NoV = dot(normal, ViewVec);
	
	// if it is backface NoV should be < 0.0f 
	if (isTwoSided && NoV < 0.0f)
	{
		//flip normal
		normal = -normal;
		NoV = dot(normal, ViewVec);
	}

	float3 HalfVec = normalize(ViewVec - gPerFrameConstants.lightDir.xyz);
	NoV = saturate(NoV);
	float NoL = dot(normal, -gPerFrameConstants.lightDir.xyz);	

	// Deal with two faced materials
	NoL = (isTwoSided ? abs(NoL) : saturate(NoL));

	// calculate color contribution from specular lighting
	float3 F0 = f3(0.08); // 0.08 is the index of refraction
	float3 SpecularColor = lerp(F0, diffuseColor.rgb, Metallic);
	float3 DiffuseColor = lerp(diffuseColor.rgb, f3(0.0), Metallic);

	// Calculate Shadow Factor
#if FT_MULTIVIEW
	float4 posLS = mul(gPerFrameVBConstants.transform[VIEW_SHADOW].vp.mat[In.ViewID], float4(position, 1.0f));
#else
	float4 posLS = mul(gPerFrameVBConstants.transform[VIEW_SHADOW].vp.mat, float4(position, 1.0f));
#endif
	float shadowFactor = calcESMShadowFactor(posLS, gShadowMap, gDepthSampler, gPerFrameConstants.esmControl);

	// Calculate Sun Illumation
	float3 shadedColor = PBR_shadowFactor(
					NoL, NoV, 
					ViewVec, HalfVec, normal, 
					DiffuseColor, SpecularColor, Roughness, Metallic, shadowFactor);
	shadedColor *= gPerFrameConstants.lightColor.rgb * gPerFrameConstants.lightColor.a * NoL * shadowFactor;
	
	if (gPerFrameConstants.visualizeAo > 0)
	{
		shadedColor = f3(ao);
	}
	
	// point lights
	// Find the light cluster for the current pixel
	uint2 clusterCoords = uint2(floor((In.screenPos * 0.5f + 0.5f) * float2(LIGHT_CLUSTER_WIDTH, LIGHT_CLUSTER_HEIGHT)));

	uint numLightsInCluster = LoadByte(gLightClustersCount, LIGHT_CLUSTER_COUNT_POS(clusterCoords.x, clusterCoords.y) << 2);

	// Accumulate light contributions
	for (uint j = 0; j < numLightsInCluster; ++j)
	{
		uint lightId = LoadByte(gLightClusters, LIGHT_CLUSTER_DATA_POS(j, clusterCoords.x, clusterCoords.y) << 2);
		shadedColor += calcPointLightShade(
						NoV, 
						ViewVec, normal,
						DiffuseColor, SpecularColor, Roughness, Metallic,
						position, gLights[lightId].position.xyz, gLights[lightId].color.xyz, LIGHT_SIZE,
						isTwoSided);
	}

	float ambientIntencity = 0.05f * ao;
	float3 ambient = diffuseColor.rgb * ambientIntencity;

	float3 FinalColor = shadedColor + ambient;
#if defined(ENABLED_GODRAY)
	FinalColor += godrayColor;
#endif

	RETURN(float4(FinalColor, 1.0));
}
