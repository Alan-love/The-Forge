/**********************************************************************
Copyright (c) 2020 Advanced Micro Devices, Inc. All rights reserved.

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
********************************************************************/

/*
 * Copyright (c) 2017-2025 The Forge Interactive Inc.
 * 
 * This file is part of The-Forge
 * (see https://github.com/ConfettiFX/The-Forge).
 * 
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 * 
 *   http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
*/

#ifndef FFX_SSSR_CLASSIFY_TILES
#define FFX_SSSR_CLASSIFY_TILES
#include "VisibiltyBufferResources.h.fsl"
#include "SSSR.srt.h"
#include "SSSR_Common.h.fsl"

GroupShared(uint, g_ray_count);
GroupShared(uint, g_ray_base_index);
GroupShared(uint, g_denoise_count);

ROOT_SIGNATURE(ComputeRootSignature)
NUM_THREADS(8,  8,  1)
void CS_MAIN( SV_DispatchThreadID(uint3) did, SV_GroupIndex(uint) group_index )
{
	INIT_MAIN;
    bool is_first_lane_of_wave = WaveIsFirstLane();
    bool is_first_lane_of_threadgroup = group_index == 0;

    // First we figure out on a per thread basis if we need to shoot a reflection ray.
    uint2 screen_size = uint2(GetDimensions(gNormalRoughness, NO_SAMPLER));

    // Disable offscreen pixels
    bool needs_ray = !(did.x >= screen_size.x || did.y >= screen_size.y);

    // Dont shoot a ray on very rough surfaces.
    float roughness = FfxSssrUnpackRoughness(LoadTex2D(gNormalRoughness, NO_SAMPLER, did.xy, 0));
    needs_ray = needs_ray && IsGlossy(roughness);

    // Also we dont need to run the denoiser on mirror reflections.
    bool needs_denoiser = needs_ray && !IsMirrorReflection(roughness);

    // Decide which ray to keep
    bool is_base_ray = IsBaseRay(did.xy, gConstants.g_samples_per_quad);
    needs_ray = needs_ray && (!needs_denoiser || is_base_ray); // Make sure to not deactivate mirror reflection rays.

    if (gConstants.g_temporal_variance_guided_tracing_enabled != 0u && needs_denoiser && !needs_ray)
    {
        float temporal_variance = LoadRWTex2D(gTemporalVariance, int2(did.xy)).x;
        bool has_temporal_variance = temporal_variance != 0.0;

        // If temporal variance is too high, we enforce a ray anyway.
        needs_ray = needs_ray || has_temporal_variance;
    }

    // Now we know for each thread if it needs to shoot a ray and wether or not a denoiser pass has to run on this pixel.
    // Thus, we need to compact the rays and append them all at once to the ray list.
    // Also, if there is at least one pixel in that tile that needs a denoiser, we have to append that tile to the tile list.

    if (is_first_lane_of_threadgroup)
    {
        g_ray_count = 0;
        g_denoise_count = 0;
    }
    GroupMemoryBarrier(); // Wait for reset to finish

    uint local_ray_index_in_wave = WavePrefixCountBits(needs_ray);
    uint wave_ray_count = WaveActiveCountBits(needs_ray);
    bool wave_needs_denoiser = WaveActiveAnyTrue(needs_denoiser);
    uint wave_count = wave_needs_denoiser ? 1 : 0;

    uint local_ray_index_of_wave = 0;
    if (is_first_lane_of_wave)
    {
        AtomicAdd(g_ray_count, wave_ray_count, local_ray_index_of_wave);
        uint _;
        AtomicAdd(g_denoise_count, wave_count, _);
    }
    local_ray_index_of_wave = WaveReadLaneFirst(local_ray_index_of_wave);

    GroupMemoryBarrier(); // Wait for ray compaction to finish

    if (is_first_lane_of_threadgroup)
    {
        bool must_denoise = g_denoise_count > 0;
        uint denoise_count = must_denoise ? 1 : 0;
        uint ray_count = g_ray_count;

        uint tile_index;
        uint ray_base_index = 0;

        AtomicAdd(gTileCounter[0], denoise_count, tile_index);
        AtomicAdd(gRayCounter[0], ray_count, ray_base_index);

        int cleaned_index = must_denoise ? int(tile_index) : -1;
	    if(must_denoise)
	    {
        	gTileList[cleaned_index] = Pack(did.xy); // Write out pixel coords of upper left pixel
	    }
        g_ray_base_index = ray_base_index;
    }
    GroupMemoryBarrier(); // Wait for ray base index to become available

    if(needs_ray)
    {
        int ray_index = int(g_ray_base_index + local_ray_index_of_wave + local_ray_index_in_wave);
        gRayList[ray_index] = Pack(did.xy); // Write out pixel to trace
    }

    if(gConstants.g_clear_targets)
    {
        // Clear intersection targets as there wont be any ray that overwrites them
        Write2D(gTemporallyDenoisedReflections, did.xy, f4(0));
    }

    Write2D(gRayLengths, did.xy, 0.0f);
    Write2D(gDenoisedReflections, did.xy, f4(0));

    // Re-purpose gTemporalVariance to hold the information for the spatial pass if a ray has been shot. Always write 0 if no denoiser is running.
    Write2D(gTemporalVariance, did.xy, needs_ray ? (1.0f - gConstants.g_skip_denoiser) : 0.0f);
	RETURN();
}
#endif // FFX_SSSR_CLASSIFY_TILES