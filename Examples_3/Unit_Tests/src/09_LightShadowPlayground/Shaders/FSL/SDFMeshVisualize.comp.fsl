/*
* Copyright (c) 2017-2025 The Forge Interactive Inc.
*
* This file is part of The-Forge
* (see https://github.com/ConfettiFX/The-Forge).
*
* Licensed to the Apache Software Foundation (ASF) under one
* or more contributor license agreements.  See the NOTICE file
* distributed with this work for additional information
* regarding copyright ownership.  The ASF licenses this file
* to you under the Apache License, Version 2.0 (the
* "License"); you may not use this file except in compliance
* with the License.  You may obtain a copy of the License at
*
*   http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing,
* software distributed under the License is distributed on an
* "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
* KIND, either express or implied.  See the License for the
* specific language governing permissions and limitations
* under the License.
*/

#include "SDFConstants.h.fsl"

#ifndef SAMPLE_COUNT
#define SAMPLE_COUNT 4
#endif

#include "VisibilityBufferResources.h.fsl"
#include "SDFMesh.srt.h"


float2 LineBoxIntersect(float3 RayOrigin, float3 RayEnd, float3 BoxMin, float3 BoxMax)
{
	float3 InvRayDir = 1.0f / (RayEnd - RayOrigin);
	float3 FirstPlaneIntersections = (BoxMin - RayOrigin) * InvRayDir;
	float3 SecondPlaneIntersections = (BoxMax - RayOrigin) * InvRayDir;
	float3 ClosestPlaneIntersections = min(FirstPlaneIntersections, SecondPlaneIntersections);
	float3 FurthestPlaneIntersections = max(FirstPlaneIntersections, SecondPlaneIntersections);

	float2 BoxIntersections;
	BoxIntersections.x = max(ClosestPlaneIntersections.x, max(ClosestPlaneIntersections.y, ClosestPlaneIntersections.z));
	BoxIntersections.y = min(FurthestPlaneIntersections.x, min(FurthestPlaneIntersections.y, FurthestPlaneIntersections.z));
	return saturate(BoxIntersections);
}

float3 SDFVolumeDimensionPositionToUV(float3 volumePos, float3 uvScale, float3 uvAdd)
{
	return volumePos * uvScale + uvAdd;
}

float SampleSDFVolumeTextureAtlas(float3 volumeUV)
{
	return SampleLvlTex3D(gSDFVolumeTextureAtlas, gSamplerTrilinearWrap, volumeUV, 0).r;
}

void RayTraceScene(float3 rayWorldStartPos, float3 rayWorldEndPos, float maxRayTime , 
	inout(float) minRayTime, inout(float) stepsTaken)
{
	minRayTime = maxRayTime;
	stepsTaken = 0;
	

	for(uint index = 0; index < gMeshSDFUniformBlock.mNumObjects[0]; ++index)
	{
		float3 volumeRayStart = mul(gMeshSDFUniformBlock.mWorldToVolumeMat[index], float4(rayWorldStartPos.xyz, 1.0)).xyz;
		float3 volumeRayEnd = mul(gMeshSDFUniformBlock.mWorldToVolumeMat[index], float4(rayWorldEndPos.xyz, 1.0)).xyz;
		float3 volumeRayDir = volumeRayEnd - volumeRayStart;
		float volumeRayLength = length(volumeRayDir);
		volumeRayDir /= volumeRayLength;

		float2 intersectionTimes = LineBoxIntersect(volumeRayStart, 
			volumeRayEnd, -gMeshSDFUniformBlock.mLocalPositionExtent[index].xyz, gMeshSDFUniformBlock.mLocalPositionExtent[index].xyz);

		if(intersectionTimes.x < intersectionTimes.y && intersectionTimes.x < 1)
		{
			//we jump straight the position of the ray when it hits the volume dimension bbox
			float sampleRayTime = intersectionTimes.x * volumeRayLength;
			
			float minDist = 1000000;
			
			uint stepIndex = 0;
			uint maxSteps = 256;
		
			LOOP
			for(; stepIndex < maxSteps; ++stepIndex)
			{
				float3 sampleVolumePos = volumeRayStart + volumeRayDir * sampleRayTime;
				float3 clampedSamplePos = clamp(sampleVolumePos, 
					-gMeshSDFUniformBlock.mLocalPositionExtent[index].xyz, gMeshSDFUniformBlock.mLocalPositionExtent[index].xyz);
				float3 volumeUV = SDFVolumeDimensionPositionToUV(clampedSamplePos,
					gMeshSDFUniformBlock.mUVScaleAndVolumeScale[index].xyz,  gMeshSDFUniformBlock.mUVAddAndSelfShadowBias[index].xyz);
			
				float sdfValue = SampleSDFVolumeTextureAtlas(volumeUV);

				minDist = min(minDist, sdfValue);

				float minStepSize = 1.0f / (4.f * maxSteps);
				float curStepDist = max(sdfValue, minStepSize);

				sampleRayTime += curStepDist;

				if(sdfValue < 0 || sampleRayTime > intersectionTimes.y * volumeRayLength)
				{
					break;
				}

			}

			if(minDist * gMeshSDFUniformBlock.mUVScaleAndVolumeScale[index].w < 0 || stepIndex == maxSteps)
			{
				minRayTime = min(minRayTime, sampleRayTime * gMeshSDFUniformBlock.mUVScaleAndVolumeScale[index].w);
			}
			stepsTaken += stepIndex;
		}
	}

}
ROOT_SIGNATURE(ComputeRootSignature)
NUM_THREADS(SDF_MESH_VISUALIZATION_THREAD_X, SDF_MESH_VISUALIZATION_THREAD_Y, 1)

void CS_MAIN( SV_GroupID(uint3) GroupID, SV_DispatchThreadID(uint3) DispatchThreadID, SV_GroupThreadID(uint3) GroupThreadID )
{
	INIT_MAIN;
	uint3 localDispatchThreadID = DispatchThreadID * SDF_SHADOW_DOWNSAMPLE_VALUE;

	float xClip = (( float(localDispatchThreadID.x) * 2.f) / gCameraUniformBlock.mWindowSize.x) - 1.f;
	float yClip = 1.f - (( float(localDispatchThreadID.y) * 2.f) / gCameraUniformBlock.mWindowSize.y);

#if SAMPLE_COUNT > 1
#if FT_MULTIVIEW
	float depthVal = LoadTex2DArrayMS(gDepthTexture, gClampToEdgeNearSampler, int3(localDispatchThreadID.xy, gPerDrawSDFData.mViewID), 0).r;
#else
	float depthVal = LoadTex2DMS(gDepthTexture, gClampToEdgeNearSampler, localDispatchThreadID.xy, 0).r;
#endif
#else
	float2 depthSize = gCameraUniformBlock.mWindowSize;
	float2 depthUV = float2(localDispatchThreadID.xy) / depthSize;
#if FT_MULTIVIEW
	float depthVal = SampleLvlTex2DArray(gDepthTexture, gClampToEdgeNearSampler, float3(depthUV, gPerDrawSDFData.mViewID), 0).r;
#else
	float depthVal = SampleLvlTex2D(gDepthTexture, gClampToEdgeNearSampler, depthUV, 0).r;
#endif
#endif

#if FT_MULTIVIEW
	float4 worldPosW =  mul(gCameraUniformBlock.InvViewProject.mat[gPerDrawSDFData.mViewID], float4(xClip, yClip, depthVal, 1.0));
#else
	float4 worldPosW =  mul(gCameraUniformBlock.InvViewProject.mat, float4(xClip, yClip, depthVal, 1.0));
#endif
	float3 worldPos = (worldPosW / worldPosW.w).xyz;
	

	float traceDistance = 40000;
	float3 cameraWorldPos = gCameraUniformBlock.mCameraPos.xyz;
	float3 rayDir = normalize( worldPos - cameraWorldPos );
	float3 rayEndPos = cameraWorldPos + rayDir * traceDistance;

	float minRayTime = traceDistance;
	float totalStepsTaken = 0;

	RayTraceScene(cameraWorldPos, rayEndPos, traceDistance, 
		minRayTime, totalStepsTaken);

	float saturatedStepsTaken = saturate(totalStepsTaken / 200.0f);

	float3 Result = float3(saturatedStepsTaken, saturatedStepsTaken, saturatedStepsTaken);


	if(minRayTime < traceDistance)
	{
		Result += 0.1f;
	}

#if FT_MULTIVIEW
	Write2DArray(gOutTexture, DispatchThreadID.xy, gPerDrawSDFData.mViewID, float4(Result, 1.0));
#else
	Write2D(gOutTexture, DispatchThreadID.xy, float4(Result, 1.0));
#endif

	RETURN();
}
